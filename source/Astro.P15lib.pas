unit Astro.P15lib;

interface

uses
  Astro.Matlib;

(*-----------------------------------------------------------------------*)
(*                                                                       *)
(* GEOCEN: geocentric coordinates (geometric and light-time corrected)   *)
(*                                                                       *)
(*   T:        time in Julian centuries since J2000                      *)
(*             T=(JD-2451545.0)/36525.0                                  *)
(*   LP,BP,RP: ecliptic heliocentric coordinates of the planet           *)
(*   LS,BS,RS: ecliptic geocentric   coordinates of the sun              *)
(*                                                                       *)
(*   IPLAN:    planet (0=Sun,1=Mercury,2=Venus,3=Earth,...,9=Pluto       *)
(*   IMODE:    desired type of coordinates (see description of X,Y,Z)    *)
(*             (0=geometric,1=astrometric,2=apparent)                    *)
(*   XP,YP,ZP: ecliptic heliocentric coordinates of the planet           *)
(*   XS,YS,ZS: ecliptic geocentric coordinates of the Sun                *)
(*   X, Y, Z : ecliptic geocentric cordinates of the planet (geometric   *)
(*             if IMODE=0, astrometric if IMODE=1, apparent if IMODE=2)  *)
(*   DELTA0:   geocentric distance (geometric)                           *)
(*                                                                       *)
(*   (all angles in degrees, distances in AU)                            *)
(*                                                                       *)
(*-----------------------------------------------------------------------*)
procedure GEOCEN(T, LP, BP, RP, LS, BS, RS: Double; IPLAN, IMODE: integer;
  var XP, YP, ZP, XS, YS, ZS, X, Y, Z, DELTA0: Double);
(*-----------------------------------------------------------------------*)
(* Mercury200: Mercury; ecliptic coordinates L,B,R (in deg and AU)           *)
(*         equinox of date                                               *)
(*         (T: time in Julian centuries since J2000)                     *)
(*         (   = (JED-2451545.0)/36525             )                     *)
(*-----------------------------------------------------------------------*)
procedure Mercury200(T: Double; var L, B, R: Double);
(*-----------------------------------------------------------------------*)
(* Venus200: Venus; ecliptic coordinates L,B,R (in deg and AU)             *)
(*         equinox of date                                               *)
(*         (T: time in Julian centuries since J2000)                     *)
(*         (   = (JED-2451545.0)/36525             )                     *)
(*-----------------------------------------------------------------------*)
procedure Venus200(T: Double; var L, B, R: Double);
(*-----------------------------------------------------------------------*)
(* Mars200: Mars; ecliptic coordinates L,B,R (in deg and AU)              *)
(*         equinox of date                                               *)
(*         (T: time in Julian centuries since J2000)                     *)
(*         (   = (JED-2451545.0)/36525             )                     *)
(*-----------------------------------------------------------------------*)
procedure Mars200(T: Double; var L, B, R: Double);
(*-----------------------------------------------------------------------*)
(* Jupiter200: Jupiter; ecliptic coordinates L,B,R (in deg and AU)           *)
(*         equinox of date                                               *)
(*         T: time in Julian centuries since J2000                       *)
(*            = (JED-2451545.0)/36525                                    *)
(*-----------------------------------------------------------------------*)
procedure Jupiter200(T: Double; var L, B, R: Double);

//=====================================================
implementation
//=====================================================

(* ----------------------------------------------------------------------- *)

procedure GEOCEN(T, LP, BP, RP, LS, BS, RS: Double; IPLAN, IMODE: integer;
  var XP, YP, ZP, XS, YS, ZS, X, Y, Z, DELTA0: Double);

const
  P2 = 6.283185307;
var
  DL, DB, DR, DLS, DBS, DRS, FAC: Double;
  VX, VY, VZ, VXS, VYS, VZS, M: Double;

  (*sub*)function Frac(X: Double): Double;
  begin
    X := X - Trunc(X);
    if (X < 0) then
      X := X + 1;
    Frac := X
  end;

  (*sub*)procedure POSVEL(L, B, R, DL, DB, DR: Double; var X, Y, Z, VX, VY, VZ: Double);
  var
    CL, SL, CB, SB: Double;
  begin
    CL := CS(L);
    SL := SN(L);
    CB := CS(B);
    SB := SN(B);
    X := R * CL * CB;
    VX := DR * CL * CB - DL * R * SL * CB - DB * R * CL * SB;
    Y := R * SL * CB;
    VY := DR * SL * CB + DL * R * CL * CB - DB * R * SL * SB;
    Z := R * SB;
    VZ := DR * SB + DB * R * CB;
  end;

begin
  DL := 0.0;
  DB := 0.0;
  DR := 0.0;
  DLS := 0.0;
  DBS := 0.0;
  DRS := 0.0;

  if (IMODE > 0) then
  begin
    M := P2 * Frac(0.9931266 + 99.9973604 * T); (* Sun *)
    DLS := 172.00 + 5.75 * Sin(M);
    DRS := 2.87 * Cos(M);
    DBS := 0.0;

    (* dl,db in 1e-4 rad/d, dr in 1e-4 AU/d *)
    case IPLAN of
      0:
        begin
          DL := 0.0;
          DB := 0.0;
          DR := 0.0;
        end; (* Sun *)
      1:
        begin (* Mercury *)
          M := P2 * Frac(0.4855407 + 415.2014314 * T);
          DL := 714.00 + 292.66 * Cos(M) + 71.96 * Cos(2 * M) + 18.16 * Cos(3 * M) + 4.61 *
            Cos(4 * M) + 3.81 * Sin(2 * M) + 2.43 * Sin(3 * M) + 1.08 * Sin(4 * M);
          DR := 55.94 * Sin(M) + 11.36 * Sin(2 * M) + 2.60 * Sin(3 * M);
          DB := 73.40 * Cos(M) + 29.82 * Cos(2 * M) + 10.22 * Cos(3 * M) + 3.28 * Cos(4 * M) - 40.44
            * Sin(M) - 16.55 * Sin(2 * M) - 5.56 * Sin(3 * M) - 1.72 * Sin(4 * M);
        end;
      2:
        begin (* Venus *)
          M := P2 * Frac(0.1400197 + 162.5494552 * T);
          DL := 280.00 + 3.79 * Cos(M);
          DR := 1.37 * Sin(M);
          DB := 9.54 * Cos(M) - 13.57 * Sin(M);
        end;
      3:
        begin
          DL := DLS;
          DR := DRS;
          DB := -DBS;
        end; (* Earth *)
      4:
        begin (* Mars *)
          M := P2 * Frac(0.0538553 + 53.1662736 * T);
          DL := 91.50 + 17.07 * Cos(M) + 2.03 * Cos(2 * M);
          DR := 12.98 * Sin(M) + 1.21 * Cos(2 * M);
          DB := 0.83 * Cos(M) + 2.80 * Sin(M);
        end;
      5:
        begin (* Jupiter *)
          M := P2 * Frac(0.0565314 + 8.4302963 * T);
          DL := 14.50 + 1.41 * Cos(M);
          DR := 3.66 * Sin(M);
          DB := 0.33 * Sin(M);
        end;
      6:
        begin (* Saturn *)
          M := P2 * Frac(0.8829867 + 3.3947688 * T);
          DL := 5.84 + 0.65 * Cos(M);
          DR := 3.09 * Sin(M);
          DB := 0.24 * Cos(M);
        end;
      7:
        begin (* Uranus *)
          M := P2 * Frac(0.3967117 + 1.1902849 * T);
          DL := 2.05 + 0.19 * Cos(M);
          DR := 1.86 * Sin(M);
          DB := -0.03 * Sin(M);
        end;
      8:
        begin (* Neptune *)
          M := P2 * Frac(0.7214906 + 0.6068526 * T);
          DL := 1.04 + 0.02 * Cos(M);
          DR := 0.27 * Sin(M);
          DB := 0.03 * Sin(M);
        end;
      9:
        begin (* Pluto *)
          M := P2 * Frac(0.0385795 + 0.4026667 * T);
          DL := 0.69 + 0.34 * Cos(M) + 0.12 * Cos(2 * M) + 0.05 * Cos(3 * M);
          DR := 6.66 * Sin(M) + 1.64 * Sin(2 * M);
          DB := -0.08 * Cos(M) - 0.17 * Sin(M) - 0.09 * Sin(2 * M);
        end;
    end;
  end;

  POSVEL(LS, BS, RS, DLS, DBS, DRS, XS, YS, ZS, VXS, VYS, VZS);
  POSVEL(LP, BP, RP, DL, DB, DR, XP, YP, ZP, VX, VY, VZ);
  X := XP + XS;
  Y := YP + YS;
  Z := ZP + ZS;
  DELTA0 := Sqrt(X * X + Y * Y + Z * Z);
  if IPLAN = 3 then
  begin
    X := 0.0;
    Y := 0.0;
    Z := 0.0;
    DELTA0 := 0.0
  end;

  FAC := 0.00578 * DELTA0 * 1E-4;
  case IMODE of
    1:
      begin
        X := X - FAC * VX;
        Y := Y - FAC * VY;
        Z := Z - FAC * VZ;
      end;
    2:
      begin
        X := X - FAC * (VX + VXS);
        Y := Y - FAC * (VY + VYS);
        Z := Z - FAC * (VZ + VZS);
      end;
  end;
end;


(* ----------------------------------------------------------------------- *)

procedure Mercury200(T: Double; var L, B, R: Double);
const
  P2 = 6.283185307;
var
  C1, S1: array [-1 .. 9] of Double;
  C, S: array [-5 .. 0] of Double;
  M1, M2, M3, M5, M6: Double;
  U, V, DL, DR, DB: Double;
  I: integer;

  (*sub*)function Frac(X: Double): Double;
  begin
    X := X - Trunc(X);
    if (X < 0) then
      X := X + 1.0;
    Frac := X
  end;

  (*sub*)procedure ADDTHE(C1, S1, C2, S2: Double; var C, S: Double);
  begin
    C := C1 * C2 - S1 * S2;
    S := S1 * C2 + C1 * S2;
  end;

  (*sub*)procedure Term(I1, I, IT: integer; DLC, DLS, DRC, DRS, DBC, DBS: Double);
  begin
    if IT = 0 then
      ADDTHE(C1[I1], S1[I1], C[I], S[I], U, V)
    else
    begin
      U := U * T;
      V := V * T
    end;
    DL := DL + DLC * U + DLS * V;
    DR := DR + DRC * U + DRS * V;
    DB := DB + DBC * U + DBS * V;
  end;

  (*sub*)procedure PERTVEN; (* Kepler terms and perturbations by Venus *)
  var
    I: integer;
  begin
    C[0] := 1.0;
    S[0] := 0.0;
    C[-1] := Cos(M2);
    S[-1] := -Sin(M2);
    for I := -1 downto -4 do
      ADDTHE(C[I], S[I], C[-1], S[-1], C[I - 1], S[I - 1]);
    Term(1, 0, 0, 259.74, 84547.39, -78342.34, 0.01, 11683.22, 21203.79);
    Term(1, 0, 1, 2.30, 5.04, -7.52, 0.02, 138.55, -71.01);
    Term(1, 0, 2, 0.01, -0.01, 0.01, 0.01, -0.19, -0.54);
    Term(2, 0, 0, -549.71, 10394.44, -7955.45, 0.00, 2390.29, 4306.79);
    Term(2, 0, 1, -4.77, 8.97, -1.53, 0.00, 28.49, -14.18);
    Term(2, 0, 2, 0.00, 0.00, 0.00, 0.00, -0.04, -0.11);
    Term(3, 0, 0, -234.04, 1748.74, -1212.86, 0.00, 535.41, 984.33);
    Term(3, 0, 1, -2.03, 3.48, -0.35, 0.00, 6.56, -2.91);
    Term(4, 0, 0, -77.64, 332.63, -219.23, 0.00, 124.40, 237.03);
    Term(4, 0, 1, -0.70, 1.10, -0.08, 0.00, 1.59, -0.59);
    Term(5, 0, 0, -23.59, 67.28, -43.54, 0.00, 29.44, 58.77);
    Term(5, 0, 1, -0.23, 0.32, -0.02, 0.00, 0.39, -0.11);
    Term(6, 0, 0, -6.86, 14.06, -9.18, 0.00, 7.03, 14.84);
    Term(6, 0, 1, -0.07, 0.09, -0.01, 0.00, 0.10, -0.02);
    Term(7, 0, 0, -1.94, 2.98, -2.02, 0.00, 1.69, 3.80);
    Term(8, 0, 0, -0.54, 0.63, -0.46, 0.00, 0.41, 0.98);
    Term(9, 0, 0, -0.15, 0.13, -0.11, 0.00, 0.10, 0.25);
    TERM(-1, -2, 0, -0.17, -0.06, -0.05, 0.14, -0.06, -0.07);
    Term(0, -1, 0, 0.24, -0.16, -0.11, -0.16, 0.04, -0.01);
    Term(0, -2, 0, -0.68, -0.25, -0.26, 0.73, -0.16, -0.18);
    Term(0, -5, 0, 0.37, 0.08, 0.06, -0.28, 0.13, 0.12);
    Term(1, -1, 0, 0.58, -0.41, 0.26, 0.36, 0.01, -0.01);
    Term(1, -2, 0, -3.51, -1.23, 0.23, -0.63, -0.05, -0.06);
    Term(1, -3, 0, 0.08, 0.53, -0.11, 0.04, 0.02, -0.09);
    Term(1, -5, 0, 1.44, 0.31, 0.30, -1.39, 0.34, 0.29);
    Term(2, -1, 0, 0.15, -0.11, 0.09, 0.12, 0.02, -0.04);
    Term(2, -2, 0, -1.99, -0.68, 0.65, -1.91, -0.20, 0.03);
    Term(2, -3, 0, -0.34, -1.28, 0.97, -0.26, 0.03, 0.03);
    Term(2, -4, 0, -0.33, 0.35, -0.13, -0.13, -0.01, 0.00);
    Term(2, -5, 0, 7.19, 1.56, -0.05, 0.12, 0.06, 0.05);
    Term(3, -2, 0, -0.52, -0.18, 0.13, -0.39, -0.16, 0.03);
    Term(3, -3, 0, -0.11, -0.42, 0.36, -0.10, -0.05, -0.05);
    Term(3, -4, 0, -0.19, 0.22, -0.23, -0.20, -0.01, 0.02);
    Term(3, -5, 0, 2.77, 0.49, -0.45, 2.56, 0.40, -0.12);
    Term(4, -5, 0, 0.67, 0.12, -0.09, 0.47, 0.24, -0.08);
    Term(5, -5, 0, 0.18, 0.03, -0.02, 0.12, 0.09, -0.03);
  end;

  (*sub*)procedure PERTEAR; (* perturbations by the Earth *)
  var
    I: integer;
  begin
    C[-1] := Cos(M3);
    S[-1] := -Sin(M3);
    for I := -1 downto -3 do
      ADDTHE(C[I], S[I], C[-1], S[-1], C[I - 1], S[I - 1]);
    Term(0, -4, 0, -0.11, -0.07, -0.08, 0.11, -0.02, -0.04);
    Term(1, -1, 0, 0.10, -0.20, 0.15, 0.07, 0.00, 0.00);
    Term(1, -2, 0, -0.35, 0.28, -0.13, -0.17, -0.01, 0.00);
    Term(1, -4, 0, -0.67, -0.45, 0.00, 0.01, -0.01, -0.01);
    Term(2, -2, 0, -0.20, 0.16, -0.16, -0.20, -0.01, 0.02);
    Term(2, -3, 0, 0.13, -0.02, 0.02, 0.14, 0.01, 0.00);
    Term(2, -4, 0, -0.33, -0.18, 0.17, -0.31, -0.04, 0.00);
  end;

  (*sub*)procedure PERTJUP; (* perturbations by Jupiter *)
  var
    I: integer;
  begin
    C[-1] := Cos(M5);
    S[-1] := -Sin(M5);
    for I := -1 downto -2 do
      ADDTHE(C[I], S[I], C[-1], S[-1], C[I - 1], S[I - 1]);
    TERM(-1, -1, 0, -0.08, 0.16, 0.15, 0.08, -0.04, 0.01);
    TERM(-1, -2, 0, 0.10, -0.06, -0.07, -0.12, 0.07, -0.01);
    Term(0, -1, 0, -0.31, 0.48, -0.02, 0.13, -0.03, -0.02);
    Term(0, -2, 0, 0.42, -0.26, -0.38, -0.50, 0.20, -0.03);
    Term(1, -1, 0, -0.70, 0.01, -0.02, -0.63, 0.00, 0.03);
    Term(1, -2, 0, 2.61, -1.97, 1.74, 2.32, 0.01, 0.01);
    Term(1, -3, 0, 0.32, -0.15, 0.13, 0.28, 0.00, 0.00);
    Term(2, -1, 0, -0.18, 0.01, 0.00, -0.13, -0.03, 0.03);
    Term(2, -2, 0, 0.75, -0.56, 0.45, 0.60, 0.08, -0.17);
    Term(3, -2, 0, 0.20, -0.15, 0.10, 0.14, 0.04, -0.08);
  end;

  (*sub*)procedure PERTSAT; (* perturbations by Saturn *)
  begin
    C[-2] := Cos(2 * M6);
    S[-2] := -Sin(2 * M6);
    Term(1, -2, 0, -0.19, 0.33, 0.00, 0.00, 0.00, 0.00);
  end;

begin (* Mercury200 *)

  DL := 0.0;
  DR := 0.0;
  DB := 0.0;
  M1 := P2 * Frac(0.4855407 + 415.2014314 * T);
  M2 := P2 * Frac(0.1394222 + 162.5490444 * T);
  M3 := P2 * Frac(0.9937861 + 99.9978139 * T);
  M5 := P2 * Frac(0.0558417 + 8.4298417 * T);
  M6 := P2 * Frac(0.8823333 + 3.3943333 * T);
  C1[0] := 1.0;
  S1[0] := 0.0;
  C1[1] := Cos(M1);
  S1[1] := Sin(M1);
  C1[-1] := C1[1];
  S1[-1] := -S1[1];
  for I := 2 to 9 do
    ADDTHE(C1[I - 1], S1[I - 1], C1[1], S1[1], C1[I], S1[I]);
  PERTVEN;
  PERTEAR;
  PERTJUP;
  PERTSAT;
  DL := DL + (2.8 + 3.2 * T);
  L := 360.0 * Frac(0.2151379 + M1 / P2 + ((5601.7 + 1.1 * T) * T + DL) / 1296.0E3);
  R := 0.3952829 + 0.0000016 * T + DR * 1.0E-6;
  B := (-2522.15 + (-30.18 + 0.04 * T) * T + DB) / 3600.0;

end; (* Mercury200 *)

(* ----------------------------------------------------------------------- *)

procedure Venus200(T: Double; var L, B, R: Double);
const
  P2 = 6.283185307;
var
  C2, S2: array [0 .. 8] of Double;
  C, S: array [-8 .. 0] of Double;
  M1, M2, M3, M4, M5, M6: Double;
  U, V, DL, DR, DB: Double;
  I: integer;

  (*sub*)function Frac(X: Double): Double;
  begin
    X := X - Trunc(X);
    if (X < 0) then
      X := X + 1.0;
    Frac := X
  end;

  (*sub*)procedure ADDTHE(C1, S1, C2, S2: Double; var C, S: Double);
  begin
    C := C1 * C2 - S1 * S2;
    S := S1 * C2 + C1 * S2;
  end;

  (*sub*)procedure Term(I1, I, IT: integer; DLC, DLS, DRC, DRS, DBC, DBS: Double);
  begin
    if IT = 0 then
      ADDTHE(C2[I1], S2[I1], C[I], S[I], U, V)
    else
    begin
      U := U * T;
      V := V * T
    end;
    DL := DL + DLC * U + DLS * V;
    DR := DR + DRC * U + DRS * V;
    DB := DB + DBC * U + DBS * V;
  end;

  (*sub*)procedure PERTMER; (* perturbations by Mercury *)
  begin
    C[0] := 1.0;
    S[0] := 0.0;
    C[-1] := Cos(M1);
    S[-1] := -Sin(M1);
    ADDTHE(C[-1], S[-1], C[-1], S[-1], C[-2], S[-2]);
    Term(1, -1, 0, 0.00, 0.00, 0.06, -0.09, 0.01, 0.00);
    Term(2, -1, 0, 0.25, -0.09, -0.09, -0.27, 0.00, 0.00);
    Term(4, -2, 0, -0.07, -0.08, -0.14, 0.14, -0.01, -0.01);
    Term(5, -2, 0, -0.35, 0.08, 0.02, 0.09, 0.00, 0.00);
  end;

  (*sub*)procedure PERTEAR; (* Kepler terms and perturbations by the Earth *)
  var
    I: integer;
  begin
    C[-1] := Cos(M3);
    S[-1] := -Sin(M3);
    for I := -1 downto -7 do
      ADDTHE(C[I], S[I], C[-1], S[-1], C[I - 1], S[I - 1]);
    Term(1, 0, 0, 2.37, 2793.23, -4899.07, 0.11, 9995.27, 7027.22);
    Term(1, 0, 1, 0.10, -19.65, 34.40, 0.22, 64.95, -86.10);
    Term(1, 0, 2, 0.06, 0.04, -0.07, 0.11, -0.55, -0.07);
    Term(2, 0, 0, -170.42, 73.13, -16.59, 0.00, 67.71, 47.56);
    Term(2, 0, 1, 0.93, 2.91, 0.23, 0.00, -0.03, -0.92);
    Term(3, 0, 0, -2.31, 0.90, -0.08, 0.00, 0.04, 2.09);
    Term(1, -1, 0, -2.38, -4.27, 3.27, -1.82, 0.00, 0.00);
    Term(1, -2, 0, 0.09, 0.00, -0.08, 0.05, -0.02, -0.25);
    Term(2, -2, 0, -9.57, -5.93, 8.57, -13.83, -0.01, -0.01);
    Term(2, -3, 0, -2.47, -2.40, 0.83, -0.95, 0.16, 0.24);
    Term(3, -2, 0, -0.09, -0.05, 0.08, -0.13, -0.28, 0.12);
    Term(3, -3, 0, 7.12, 0.32, -0.62, 13.76, -0.07, 0.01);
    Term(3, -4, 0, -0.65, -0.17, 0.18, -0.73, 0.10, 0.05);
    Term(3, -5, 0, -1.08, -0.95, -0.17, 0.22, -0.03, -0.03);
    Term(4, -3, 0, 0.06, 0.00, -0.01, 0.08, 0.14, -0.18);
    Term(4, -4, 0, 0.93, -0.46, 1.06, 2.13, -0.01, 0.01);
    Term(4, -5, 0, -1.53, 0.38, -0.64, -2.54, 0.27, 0.00);
    Term(4, -6, 0, -0.17, -0.05, 0.03, -0.11, 0.02, 0.00);
    Term(5, -5, 0, 0.18, -0.28, 0.71, 0.47, -0.02, 0.04);
    Term(5, -6, 0, 0.15, -0.14, 0.30, 0.31, -0.04, 0.03);
    Term(5, -7, 0, -0.08, 0.02, -0.03, -0.11, 0.01, 0.00);
    Term(5, -8, 0, -0.23, 0.00, 0.01, -0.04, 0.00, 0.00);
    Term(6, -6, 0, 0.01, -0.14, 0.39, 0.04, 0.00, -0.01);
    Term(6, -7, 0, 0.02, -0.05, 0.12, 0.04, -0.01, 0.01);
    Term(6, -8, 0, 0.10, -0.10, 0.19, 0.19, -0.02, 0.02);
    Term(7, -7, 0, -0.03, -0.06, 0.18, -0.08, 0.00, 0.00);
    Term(8, -8, 0, -0.03, -0.02, 0.06, -0.08, 0.00, 0.00);
  end;

  (*sub*)procedure PERTMAR; (* perturbations by Mars *)
  var
    I: integer;
  begin
    C[-1] := Cos(M4);
    S[-1] := -Sin(M4);
    for I := -1 downto -2 do
      ADDTHE(C[I], S[I], C[-1], S[-1], C[I - 1], S[I - 1]);
    Term(1, -3, 0, -0.65, 1.02, -0.04, -0.02, -0.02, 0.00);
    Term(2, -2, 0, -0.05, 0.04, -0.09, -0.10, 0.00, 0.00);
    Term(2, -3, 0, -0.50, 0.45, -0.79, -0.89, 0.01, 0.03);
  end;

  (*sub*)procedure PERTJUP; (* perturbations by Jupiter *)
  var
    I: integer;
  begin
    C[-1] := Cos(M5);
    S[-1] := -Sin(M5);
    for I := -1 downto -2 do
      ADDTHE(C[I], S[I], C[-1], S[-1], C[I - 1], S[I - 1]);
    Term(0, -1, 0, -0.05, 1.56, 0.16, 0.04, -0.08, -0.04);
    Term(1, -1, 0, -2.62, 1.40, -2.35, -4.40, 0.02, 0.03);
    Term(1, -2, 0, -0.47, -0.08, 0.12, -0.76, 0.04, -0.18);
    Term(2, -2, 0, -0.73, -0.51, 1.27, -1.82, -0.01, 0.01);
    Term(2, -3, 0, -0.14, -0.10, 0.25, -0.34, 0.00, 0.00);
    Term(3, -3, 0, -0.01, 0.04, -0.11, -0.02, 0.00, 0.00);
  end;

  (*sub*)procedure PERTSAT; (* perturbations by Saturn *)
  begin
    C[-1] := Cos(M6);
    S[-1] := -Sin(M6);
    Term(0, -1, 0, 0.00, 0.21, 0.00, 0.00, 0.00, -0.01);
    Term(1, -1, 0, -0.11, -0.14, 0.24, -0.20, 0.01, 0.00);
  end;

begin (* Venus200 *)
  DL := 0.0;
  DR := 0.0;
  DB := 0.0;
  M1 := P2 * Frac(0.4861431 + 415.2018375 * T);
  M2 := P2 * Frac(0.1400197 + 162.5494552 * T);
  M3 := P2 * Frac(0.9944153 + 99.9982208 * T);
  M4 := P2 * Frac(0.0556297 + 53.1674631 * T);
  M5 := P2 * Frac(0.0567028 + 8.4305083 * T);
  M6 := P2 * Frac(0.8830539 + 3.3947206 * T);
  C2[0] := 1.0;
  S2[0] := 0.0;
  C2[1] := Cos(M2);
  S2[1] := Sin(M2);
  for I := 2 to 8 do
    ADDTHE(C2[I - 1], S2[I - 1], C2[1], S2[1], C2[I], S2[I]);
  PERTMER;
  PERTEAR;
  PERTMAR;
  PERTJUP;
  PERTSAT;
  DL := DL + 2.74 * Sin(P2 * (0.0764 + 0.4174 * T)) + 0.27 * Sin(P2 * (0.9201 + 0.3307 * T));
  DL := DL + (1.9 + 1.8 * T);
  L := 360.0 * Frac(0.3654783 + M2 / P2 + ((5071.2 + 1.1 * T) * T + DL) / 1296.0E3);
  R := 0.7233482 - 0.0000002 * T + DR * 1.0E-6;
  B := (-67.70 + (0.04 + 0.01 * T) * T + DB) / 3600.0;

end; (* Venus200 *)


(* ----------------------------------------------------------------------- *)

procedure Mars200(T: Double; var L, B, R: Double);
const
  P2 = 6.283185307;
var
  C4, S4: array [-2 .. 16] of Double;
  C, S: array [-9 .. 0] of Double;
  M2, M3, M4, M5, M6: Double;
  U, V, DL, DR, DB: Double;
  I: integer;

  (*sub*)function Frac(X: Double): Double;
  begin
    X := X - Trunc(X);
    if (X < 0) then
      X := X + 1.0;
    Frac := X
  end;

  (*sub*)procedure ADDTHE(C1, S1, C2, S2: Double; var C, S: Double);
  begin
    C := C1 * C2 - S1 * S2;
    S := S1 * C2 + C1 * S2;
  end;

  (*sub*)procedure Term(I1, I, IT: integer; DLC, DLS, DRC, DRS, DBC, DBS: Double);
  begin
    if IT = 0 then
      ADDTHE(C4[I1], S4[I1], C[I], S[I], U, V)
    else
    begin
      U := U * T;
      V := V * T
    end;
    DL := DL + DLC * U + DLS * V;
    DR := DR + DRC * U + DRS * V;
    DB := DB + DBC * U + DBS * V;
  end;

  (*sub*)procedure PERTVEN; (* perturbations by Venus *)
  begin
    C[0] := 1.0;
    S[0] := 0.0;
    C[-1] := Cos(M2);
    S[-1] := -Sin(M2);
    ADDTHE(C[-1], S[-1], C[-1], S[-1], C[-2], S[-2]);
    Term(0, -1, 0, -0.01, -0.03, 0.10, -0.04, 0.00, 0.00);
    Term(1, -1, 0, 0.05, 0.10, -2.08, 0.75, 0.00, 0.00);
    Term(2, -1, 0, -0.25, -0.57, -2.58, 1.18, 0.05, -0.04);
    Term(2, -2, 0, 0.02, 0.02, 0.13, -0.14, 0.00, 0.00);
    Term(3, -1, 0, 3.41, 5.38, 1.87, -1.15, 0.01, -0.01);
    Term(3, -2, 0, 0.02, 0.02, 0.11, -0.13, 0.00, 0.00);
    Term(4, -1, 0, 0.32, 0.49, -1.88, 1.21, -0.07, 0.07);
    Term(4, -2, 0, 0.03, 0.03, 0.12, -0.14, 0.00, 0.00);
    Term(5, -1, 0, 0.04, 0.06, -0.17, 0.11, -0.01, 0.01);
    Term(5, -2, 0, 0.11, 0.09, 0.35, -0.43, -0.01, 0.01);
    Term(6, -2, 0, -0.36, -0.28, -0.20, 0.25, 0.00, 0.00);
    Term(7, -2, 0, -0.03, -0.03, 0.11, -0.13, 0.00, -0.01);
  end;

  (*sub*)procedure PERTEAR; (* Kepler terms and perturbations by the Earth *)
  var
    I: integer;
  begin
    C[-1] := Cos(M3);
    S[-1] := -Sin(M3);
    for I := -1 downto -8 do
      ADDTHE(C[I], S[I], C[-1], S[-1], C[I - 1], S[I - 1]);
    Term(1, 0, 0, -5.32, 38481.97, -141856.04, 0.40, -6321.67, 1876.89);
    Term(1, 0, 1, -1.12, 37.98, -138.67, -2.93, 37.28, 117.48);
    Term(1, 0, 2, -0.32, -0.03, 0.12, -1.19, 1.04, -0.40);
    Term(2, 0, 0, 28.28, 2285.80, -6608.37, 0.00, -589.35, 174.81);
    Term(2, 0, 1, 1.64, 3.37, -12.93, 0.00, 2.89, 11.10);
    Term(2, 0, 2, 0.00, 0.00, 0.00, 0.00, 0.10, -0.03);
    Term(3, 0, 0, 5.31, 189.29, -461.81, 0.00, -61.98, 18.53);
    Term(3, 0, 1, 0.31, 0.35, -1.36, 0.00, 0.25, 1.19);
    Term(4, 0, 0, 0.81, 17.96, -38.26, 0.00, -6.88, 2.08);
    Term(4, 0, 1, 0.05, 0.04, -0.15, 0.00, 0.02, 0.14);
    Term(5, 0, 0, 0.11, 1.83, -3.48, 0.00, -0.79, 0.24);
    Term(6, 0, 0, 0.02, 0.20, -0.34, 0.00, -0.09, 0.03);
    TERM(-1, -1, 0, 0.09, 0.06, 0.14, -0.22, 0.02, -0.02);
    Term(0, -1, 0, 0.72, 0.49, 1.55, -2.31, 0.12, -0.10);
    Term(1, -1, 0, 7.00, 4.92, 13.93, -20.48, 0.08, -0.13);
    Term(2, -1, 0, 13.08, 4.89, -4.53, 10.01, -0.05, 0.13);
    Term(2, -2, 0, 0.14, 0.05, -0.48, -2.66, 0.01, 0.14);
    Term(3, -1, 0, 1.38, 0.56, -2.00, 4.85, -0.01, 0.19);
    Term(3, -2, 0, -6.85, 2.68, 8.38, 21.42, 0.00, 0.03);
    Term(3, -3, 0, -0.08, 0.20, 1.20, 0.46, 0.00, 0.00);
    Term(4, -1, 0, 0.16, 0.07, -0.19, 0.47, -0.01, 0.05);
    Term(4, -2, 0, -4.41, 2.14, -3.33, -7.21, -0.07, -0.09);
    Term(4, -3, 0, -0.12, 0.33, 2.22, 0.72, -0.03, -0.02);
    Term(4, -4, 0, -0.04, -0.06, -0.36, 0.23, 0.00, 0.00);
    Term(5, -2, 0, -0.44, 0.21, -0.70, -1.46, -0.06, -0.07);
    Term(5, -3, 0, 0.48, -2.60, -7.25, -1.37, 0.00, 0.00);
    Term(5, -4, 0, -0.09, -0.12, -0.66, 0.50, 0.00, 0.00);
    Term(5, -5, 0, 0.03, 0.00, 0.01, -0.17, 0.00, 0.00);
    Term(6, -2, 0, -0.05, 0.03, -0.07, -0.15, -0.01, -0.01);
    Term(6, -3, 0, 0.10, -0.96, 2.36, 0.30, 0.04, 0.00);
    Term(6, -4, 0, -0.17, -0.20, -1.09, 0.94, 0.02, -0.02);
    Term(6, -5, 0, 0.05, 0.00, 0.00, -0.30, 0.00, 0.00);
    Term(7, -3, 0, 0.01, -0.10, 0.32, 0.04, 0.02, 0.00);
    Term(7, -4, 0, 0.86, 0.77, 1.86, -2.01, 0.01, -0.01);
    Term(7, -5, 0, 0.09, -0.01, -0.05, -0.44, 0.00, 0.00);
    Term(7, -6, 0, -0.01, 0.02, 0.10, 0.08, 0.00, 0.00);
    Term(8, -4, 0, 0.20, 0.16, -0.53, 0.64, -0.01, 0.02);
    Term(8, -5, 0, 0.17, -0.03, -0.14, -0.84, 0.00, 0.01);
    Term(8, -6, 0, -0.02, 0.03, 0.16, 0.09, 0.00, 0.00);
    Term(9, -5, 0, -0.55, 0.15, 0.30, 1.10, 0.00, 0.00);
    Term(9, -6, 0, -0.02, 0.04, 0.20, 0.10, 0.00, 0.00);
    Term(10, -5, 0, -0.09, 0.03, -0.10, -0.33, 0.00, -0.01);
    Term(10, -6, 0, -0.05, 0.11, 0.48, 0.21, -0.01, 0.00);
    Term(11, -6, 0, 0.10, -0.35, -0.52, -0.15, 0.00, 0.00);
    Term(11, -7, 0, -0.01, -0.02, -0.10, 0.07, 0.00, 0.00);
    Term(12, -6, 0, 0.01, -0.04, 0.18, 0.04, 0.01, 0.00);
    Term(12, -7, 0, -0.05, -0.07, -0.29, 0.20, 0.01, 0.00);
    Term(13, -7, 0, 0.23, 0.27, 0.25, -0.21, 0.00, 0.00);
    Term(14, -7, 0, 0.02, 0.03, -0.10, 0.09, 0.00, 0.00);
    Term(14, -8, 0, 0.05, 0.01, 0.03, -0.23, 0.00, 0.03);
    Term(15, -8, 0, -1.53, 0.27, 0.06, 0.42, 0.00, 0.00);
    Term(16, -8, 0, -0.14, 0.02, -0.10, -0.55, -0.01, -0.02);
    Term(16, -9, 0, 0.03, -0.06, -0.25, -0.11, 0.00, 0.00);
  end;

  (*sub*)procedure PERTJUP; (* perturbations by Jupiter *)
  var
    I: integer;
  begin
    C[-1] := Cos(M5);
    S[-1] := -Sin(M5);
    for I := -1 downto -4 do
      ADDTHE(C[I], S[I], C[-1], S[-1], C[I - 1], S[I - 1]);
    TERM(-2, -1, 0, 0.05, 0.03, 0.08, -0.14, 0.01, -0.01);
    TERM(-1, -1, 0, 0.39, 0.27, 0.92, -1.50, -0.03, -0.06);
    TERM(-1, -2, 0, -0.16, 0.03, 0.13, 0.67, -0.01, 0.06);
    TERM(-1, -3, 0, -0.02, 0.01, 0.05, 0.09, 0.00, 0.01);
    Term(0, -1, 0, 3.56, 1.13, -5.41, -7.18, -0.25, -0.24);
    Term(0, -2, 0, -1.44, 0.25, 1.24, 7.96, 0.02, 0.31);
    Term(0, -3, 0, -0.21, 0.11, 0.55, 1.04, 0.01, 0.05);
    Term(0, -4, 0, -0.02, 0.02, 0.11, 0.11, 0.00, 0.01);
    Term(1, -1, 0, 16.67, -19.15, 61.00, 53.36, -0.06, -0.07);
    Term(1, -2, 0, -21.64, 3.18, -7.77, -54.64, -0.31, 0.50);
    Term(1, -3, 0, -2.82, 1.45, -2.53, -5.73, 0.01, 0.07);
    Term(1, -4, 0, -0.31, 0.28, -0.34, -0.51, 0.00, 0.00);
    Term(2, -1, 0, 2.15, -2.29, 7.04, 6.94, 0.33, 0.19);
    Term(2, -2, 0, -15.69, 3.31, -15.70, -73.17, -0.17, -0.25);
    Term(2, -3, 0, -1.73, 1.95, -9.19, -7.20, 0.02, -0.03);
    Term(2, -4, 0, -0.01, 0.33, -1.42, 0.08, 0.01, -0.01);
    Term(2, -5, 0, 0.03, 0.03, -0.13, 0.12, 0.00, 0.00);
    Term(3, -1, 0, 0.26, -0.28, 0.73, 0.71, 0.08, 0.04);
    Term(3, -2, 0, -2.06, 0.46, -1.61, -6.72, -0.13, -0.25);
    Term(3, -3, 0, -1.28, -0.27, 2.21, -6.90, -0.04, -0.02);
    Term(3, -4, 0, -0.22, 0.08, -0.44, -1.25, 0.00, 0.01);
    Term(3, -5, 0, -0.02, 0.03, -0.15, -0.08, 0.00, 0.00);
    Term(4, -1, 0, 0.03, -0.03, 0.08, 0.08, 0.01, 0.01);
    Term(4, -2, 0, -0.26, 0.06, -0.17, -0.70, -0.03, -0.05);
    Term(4, -3, 0, -0.20, -0.05, 0.22, -0.79, -0.01, -0.02);
    Term(4, -4, 0, -0.11, -0.14, 0.93, -0.60, 0.00, 0.00);
    Term(4, -5, 0, -0.04, -0.02, 0.09, -0.23, 0.00, 0.00);
    Term(5, -4, 0, -0.02, -0.03, 0.13, -0.09, 0.00, 0.00);
    Term(5, -5, 0, 0.00, -0.03, 0.21, 0.01, 0.00, 0.00);
  end;

  (*sub*)procedure PERTSAT; (* perturbations by Saturn *)
  var
    I: integer;
  begin
    C[-1] := Cos(M6);
    S[-1] := -Sin(M6);
    for I := -1 downto -3 do
      ADDTHE(C[I], S[I], C[-1], S[-1], C[I - 1], S[I - 1]);
    TERM(-1, -1, 0, 0.03, 0.13, 0.48, -0.13, 0.02, 0.00);
    Term(0, -1, 0, 0.27, 0.84, 0.40, -0.43, 0.01, -0.01);
    Term(0, -2, 0, 0.12, -0.04, -0.33, -0.55, -0.01, -0.02);
    Term(0, -3, 0, 0.02, -0.01, -0.07, -0.08, 0.00, 0.00);
    Term(1, -1, 0, 1.12, 0.76, -2.66, 3.91, -0.01, 0.01);
    Term(1, -2, 0, 1.49, -0.95, 3.07, 4.83, 0.04, -0.05);
    Term(1, -3, 0, 0.21, -0.18, 0.55, 0.64, 0.00, 0.00);
    Term(2, -1, 0, 0.12, 0.10, -0.29, 0.34, -0.01, 0.02);
    Term(2, -2, 0, 0.51, -0.36, 1.61, 2.25, 0.03, 0.01);
    Term(2, -3, 0, 0.10, -0.10, 0.50, 0.43, 0.00, 0.00);
    Term(2, -4, 0, 0.01, -0.02, 0.11, 0.05, 0.00, 0.00);
    Term(3, -2, 0, 0.07, -0.05, 0.16, 0.22, 0.01, 0.01);
  end;

begin (* Mars200 *)

  DL := 0.0;
  DR := 0.0;
  DB := 0.0;
  M2 := P2 * Frac(0.1382208 + 162.5482542 * T);
  M3 := P2 * Frac(0.9926208 + 99.9970236 * T);
  M4 := P2 * Frac(0.0538553 + 53.1662736 * T);
  M5 := P2 * Frac(0.0548944 + 8.4290611 * T);
  M6 := P2 * Frac(0.8811167 + 3.3935250 * T);
  C4[0] := 1.0;
  S4[0] := 0.0;
  C4[1] := Cos(M4);
  S4[1] := Sin(M4);
  for I := 2 to 16 do
    ADDTHE(C4[I - 1], S4[I - 1], C4[1], S4[1], C4[I], S4[I]);
  for I := -2 to -1 do
  begin
    C4[I] := C4[-I];
    S4[I] := -S4[-I]
  end;
  PERTVEN;
  PERTEAR;
  PERTJUP;
  PERTSAT;
  DL := DL + 52.49 * Sin(P2 * (0.1868 + 0.0549 * T)) + 0.61 * Sin(P2 * (0.9220 + 0.3307 * T)) + 0.32
    * Sin(P2 * (0.4731 + 2.1485 * T)) + 0.28 * Sin(P2 * (0.9467 + 0.1133 * T));
  DL := DL + (0.14 + 0.87 * T - 0.11 * T * T);
  L := 360.0 * Frac(0.9334591 + M4 / P2 + ((6615.5 + 1.1 * T) * T + DL) / 1296.0E3);
  R := 1.5303352 + 0.0000131 * T + DR * 1.0E-6;
  B := (596.32 + (-2.92 - 0.10 * T) * T + DB) / 3600.0;
end;

(* ----------------------------------------------------------------------- *)
procedure Jupiter200(T: Double; var L, B, R: Double);
const
  P2 = 6.283185307;
var
  C5, S5: array [-1 .. 5] of Double;
  C, S: array [-10 .. 0] of Double;
  M5, M6, M7: Double;
  U, V, DL, DR, DB: Double;
  I: integer;

  (*sub*)function Frac(X: Double): Double;
  begin
    X := X - Trunc(X);
    if (X < 0) then
      X := X + 1.0;
    Frac := X
  end;

  (*sub*)procedure ADDTHE(C1, S1, C2, S2: Double; var C, S: Double);
  begin
    C := C1 * C2 - S1 * S2;
    S := S1 * C2 + C1 * S2;
  end;

  (*sub*)procedure Term(I5, I, IT: integer; DLC, DLS, DRC, DRS, DBC, DBS: Double);
  begin
    if IT = 0 then
      ADDTHE(C5[I5], S5[I5], C[I], S[I], U, V)
    else
    begin
      U := U * T;
      V := V * T
    end;
    DL := DL + DLC * U + DLS * V;
    DR := DR + DRC * U + DRS * V;
    DB := DB + DBC * U + DBS * V;
  end;

  (*sub*)procedure PERTSAT; (* Kepler terms and perturbations by Saturn *)
  var
    I: integer;
  begin
    C[0] := 1.0;
    S[0] := 0.0;
    C[-1] := Cos(M6);
    S[-1] := -Sin(M6);
    for I := -1 downto -9 do
      ADDTHE(C[I], S[I], C[-1], S[-1], C[I - 1], S[I - 1]);
    TERM(-1, -1, 0, -0.2, 1.4, 2.0, 0.6, 0.1, -0.2);
    Term(0, -1, 0, 9.4, 8.9, 3.9, -8.3, -0.4, -1.4);
    Term(0, -2, 0, 5.6, -3.0, -5.4, -5.7, -2.0, 0.0);
    Term(0, -3, 0, -4.0, -0.1, 0.0, 5.5, 0.0, 0.0);
    Term(0, -5, 0, 3.3, -1.6, -1.6, -3.1, -0.5, -1.2);
    Term(1, 0, 0, -113.1, 19998.6, -25208.2, -142.2, -4670.7, 288.9);
    Term(1, 0, 1, -76.1, 66.9, -84.2, -95.8, 21.6, 29.4);
    Term(1, 0, 2, -0.5, -0.3, 0.4, -0.7, 0.1, -0.1);
    Term(1, -1, 0, 78.8, -14.5, 11.5, 64.4, -0.2, 0.2);
    Term(1, -2, 0, -2.0, -132.4, 28.8, 4.3, -1.7, 0.4);
    Term(1, -2, 1, -1.1, -0.7, 0.2, -0.3, 0.0, 0.0);
    Term(1, -3, 0, -7.5, -6.8, -0.4, -1.1, 0.6, -0.9);
    Term(1, -4, 0, 0.7, 0.7, 0.6, -1.1, 0.0, -0.2);
    Term(1, -5, 0, 51.5, -26.0, -32.5, -64.4, -4.9, -12.4);
    Term(1, -5, 1, -1.2, -2.2, -2.7, 1.5, -0.4, 0.3);
    Term(2, 0, 0, -3.4, 632.0, -610.6, -6.5, -226.8, 12.7);
    Term(2, 0, 1, -4.2, 3.8, -4.1, -4.5, 0.2, 0.6);
    Term(2, -1, 0, 5.3, -0.7, 0.7, 6.1, 0.2, 1.1);
    Term(2, -2, 0, -76.4, -185.1, 260.2, -108.0, 1.6, 0.0);
    Term(2, -3, 0, 66.7, 47.8, -51.4, 69.8, 0.9, 0.3);
    Term(2, -3, 1, 0.6, -1.0, 1.0, 0.6, 0.0, 0.0);
    Term(2, -4, 0, 17.0, 1.4, -1.8, 9.6, 0.0, -0.1);
    Term(2, -5, 0, 1066.2, -518.3, -1.3, -23.9, 1.8, -0.3);
    Term(2, -5, 1, -25.4, -40.3, -0.9, 0.3, 0.0, 0.0);
    Term(2, -5, 2, -0.7, 0.5, 0.0, 0.0, 0.0, 0.0);
    Term(3, 0, 0, -0.1, 28.0, -22.1, -0.2, -12.5, 0.7);
    Term(3, -2, 0, -5.0, -11.5, 11.7, -5.4, 2.1, -1.0);
    Term(3, -3, 0, 16.9, -6.4, 13.4, 26.9, -0.5, 0.8);
    Term(3, -4, 0, 7.2, -13.3, 20.9, 10.5, 0.1, -0.1);
    Term(3, -5, 0, 68.5, 134.3, -166.9, 86.5, 7.1, 15.2);
    Term(3, -5, 1, 3.5, -2.7, 3.4, 4.3, 0.5, -0.4);
    Term(3, -6, 0, 0.6, 1.0, -0.9, 0.5, 0.0, 0.0);
    Term(3, -7, 0, -1.1, 1.7, -0.4, -0.2, 0.0, 0.0);
    Term(4, 0, 0, 0.0, 1.4, -1.0, 0.0, -0.6, 0.0);
    Term(4, -2, 0, -0.3, -0.7, 0.4, -0.2, 0.2, -0.1);
    Term(4, -3, 0, 1.1, -0.6, 0.9, 1.2, 0.1, 0.2);
    Term(4, -4, 0, 3.2, 1.7, -4.1, 5.8, 0.2, 0.1);
    Term(4, -5, 0, 6.7, 8.7, -9.3, 8.7, -1.1, 1.6);
    Term(4, -6, 0, 1.5, -0.3, 0.6, 2.4, 0.0, 0.0);
    Term(4, -7, 0, -1.9, 2.3, -3.2, -2.7, 0.0, -0.1);
    Term(4, -8, 0, 0.4, -1.8, 1.9, 0.5, 0.0, 0.0);
    Term(4, -9, 0, -0.2, -0.5, 0.3, -0.1, 0.0, 0.0);
    Term(4, -10, 0, -8.6, -6.8, -0.4, 0.1, 0.0, 0.0);
    Term(4, -10, 1, -0.5, 0.6, 0.0, 0.0, 0.0, 0.0);
    Term(5, -5, 0, -0.1, 1.5, -2.5, -0.8, -0.1, 0.1);
    Term(5, -6, 0, 0.1, 0.8, -1.6, 0.1, 0.0, 0.0);
    Term(5, -9, 0, -0.5, -0.1, 0.1, -0.8, 0.0, 0.0);
    Term(5, -10, 0, 2.5, -2.2, 2.8, 3.1, 0.1, -0.2);
  end;

  (*sub*)procedure PERTURA; (* perturbations by Uranus *)
  begin
    C[-1] := Cos(M7);
    S[-1] := -Sin(M7);
    ADDTHE(C[-1], S[-1], C[-1], S[-1], C[-2], S[-2]);
    Term(1, -1, 0, 0.4, 0.9, 0.0, 0.0, 0.0, 0.0);
    Term(1, -2, 0, 0.4, 0.4, -0.4, 0.3, 0.0, 0.0);
  end;

  (*sub*)procedure PERTSUR; (* perturbations by Saturn and Uranus *)
  var
    PHI, X, Y: Double;
  begin
    PHI := (2 * M5 - 6 * M6 + 3 * M7);
    X := Cos(PHI);
    Y := Sin(PHI);
    DL := DL - 0.8 * X + 8.5 * Y;
    DR := DR - 0.1 * X;
    ADDTHE(X, Y, C5[1], S5[1], X, Y);
    DL := DL + 0.4 * X + 0.5 * Y;
    DR := DR - 0.7 * X + 0.5 * Y;
    DB := DB - 0.1 * X;
  end;

begin (* Jupiter200 *)

  DL := 0.0;
  DR := 0.0;
  DB := 0.0;
  M5 := P2 * Frac(0.0565314 + 8.4302963 * T);
  M6 := P2 * Frac(0.8829867 + 3.3947688 * T);
  M7 := P2 * Frac(0.3969537 + 1.1902586 * T);
  C5[0] := 1.0;
  S5[0] := 0.0;
  C5[1] := Cos(M5);
  S5[1] := Sin(M5);
  C5[-1] := C5[1];
  S5[-1] := -S5[1];
  for I := 2 to 5 do
    ADDTHE(C5[I - 1], S5[I - 1], C5[1], S5[1], C5[I], S5[I]);
  PERTSAT;
  PERTURA;
  PERTSUR;
  L := 360.0 * Frac(0.0388910 + M5 / P2 + ((5025.2 + 0.8 * T) * T + DL) / 1296.0E3);
  R := 5.208873 + 0.000041 * T + DR * 1.0E-5;
  B := (227.3 - 0.3 * T + DB) / 3600.0;

end; (* Jupiter200 *)




end.
